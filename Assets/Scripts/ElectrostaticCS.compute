struct Speck
{
	float3 Pos;
	float3 Vel;
	int D;
};

RWStructuredBuffer<Speck> Specks;
RWStructuredBuffer<Speck> SortedSpecks;
RWStructuredBuffer<uint> PartCounts;
RWStructuredBuffer<uint2> PartIndices;
RWStructuredBuffer<int> PartIndicesX;
RWStructuredBuffer<int> PartIndicesY;

RWStructuredBuffer<uint> PfxSumA;
RWStructuredBuffer<uint> PfxSumB;
uint PfxSumPower;

float K;
float A;
float B;
float C;
float D;
float E;
float F;

uint NumSpecks;
float Damper;
float MaxVel;
float MaxDist;
float Bounds;
uint PartsPerDim;
uint NumParts;
float Multiplier;
float DeltaTime;

// Boundary conditions
float KBoundary;
float Restitution;

float3 Gravity;

float Attraction(float x)
{
	float rt = (B * x) - C;
	return (K * A) / (rt * rt);
}

float Repulsion(float x)
{
	float rt = (F * x) - E;
	return (K * D) / (rt * rt);
}

float3 ProjectOnPlane(float3 vec, float3 normal)
{
    return vec - (normal * dot(vec, normal));
}

uint3 GetPartCoords(float3 pos)
{
	pos += float3(1, 1, 1) * (Bounds / 2);
	pos /= MaxDist;
	return ceil(pos) - 1;
}

uint GetPartitionIdx(float3 pos)
{
	uint3 coords = GetPartCoords(pos);
	return coords.x + (coords.y * PartsPerDim) + (coords.z * PartsPerDim * PartsPerDim);
}

void CalculateBoundaryForce(int id)
{
	float3 pos = Specks[id].Pos;

	float limit = Bounds / 2;
	float3 plusDist = max((-pos + limit), 0.001);
	float3 negDist = max((pos + limit), 0.001);

	float3 pSq = plusDist * plusDist;
	float3 nSq = negDist * negDist;

	float3 a = (KBoundary / nSq) - (KBoundary / pSq);
	float3 dv = a * DeltaTime;
	float3 maxDV = abs(Specks[id].Vel * (1 + Restitution));
	dv = clamp(dv, -maxDV, maxDV);
	
	Specks[id].Vel += dv;
}

#pragma kernel UpdateVelocities
[numthreads(256, 1, 1)]
void UpdateVelocities (uint3 id : SV_DispatchThreadID)
{
	if (id.x >= NumSpecks)
		return;

	float3 force = float3(0, 0, 0);
	int numNeighbours = 0;
	Speck thisSpeck = Specks[id.x];

	uint partIdx = PartIndices[id.x].x;
	uint subPartIdx = PartIndices[id.x].y;
	int partOffset = PfxSumB[partIdx] - PartCounts[partIdx];
	uint sortedIdx = partOffset + subPartIdx;

	uint partsPerPlane = PartsPerDim * PartsPerDim;

	int3 partCoords = GetPartCoords(thisSpeck.Pos);

	int xMid = partIdx % PartsPerDim;
	int yMid = (partIdx / PartsPerDim) % PartsPerDim;
	int zMid = (partIdx / partsPerPlane);

	for	(int z = zMid - 1; z <= zMid + 1; z++)
	{
		for	(int y = yMid - 1; y <= yMid + 1; y++)
		{
			for	(int x = xMid - 1; x <= xMid + 1; x++)
			{
				uint partIdx = (partsPerPlane * z) + (PartsPerDim * y) + x;
				uint partStart = PfxSumB[partIdx] - PartCounts[partIdx];
				uint partEnd = PfxSumB[partIdx];

				for	(uint i = partStart; i < partEnd; i++)
				{
					Speck otherSpeck = SortedSpecks[i];

					float dist = distance(thisSpeck.Pos, otherSpeck.Pos);

					if (isnan(dist) || i == sortedIdx || dist > MaxDist)
						continue;

					float forceMag = Attraction(dist) - Repulsion(dist);
					float3 toOther = normalize(otherSpeck.Pos - thisSpeck.Pos);

					if (dist == 0)
					{
						toOther = normalize(thisSpeck.Pos) * (-1 * (sortedIdx < i));
					}

					force += forceMag * toOther;
					numNeighbours++;
				}
			}
		}
	}

	Specks[id.x].D = numNeighbours;

	float3 a = (force * Multiplier) + Gravity;
	Specks[id.x].Vel += a * DeltaTime;
	CalculateBoundaryForce(id.x);

	Specks[id.x].Vel -= Damper * DeltaTime * Specks[id.x].Vel;

	if (length(Specks[id.x].Vel) > MaxVel)
	{
		Specks[id.x].Vel = normalize(Specks[id.x].Vel) * MaxVel;
	}
}

#pragma kernel UpdatePositions
[numthreads(256, 1, 1)]
void UpdatePositions (uint3 id : SV_DispatchThreadID)
{
	if (id.x >= NumSpecks)
		return;

	Specks[id.x].Pos += Specks[id.x].Vel * DeltaTime;
}

#pragma kernel PopulatePartitions
[numthreads(256, 1, 1)]
void PopulatePartitions (uint id : SV_DispatchThreadID)
{
	if (id.x >= NumSpecks)
		return;

	uint partIdx = GetPartitionIdx(Specks[id.x].Pos);
	PartIndices[id.x].x = partIdx;
	InterlockedAdd(PartCounts[partIdx], 1, PartIndices[id.x].y);
	InterlockedAdd(PfxSumA[partIdx], 1);
}

#pragma kernel SumPartCounts
[numthreads(256, 1, 1)]
void SumPartCounts (uint id : SV_DispatchThreadID)
{
	if (id.x >= NumParts)
		return;

	PfxSumB[id.x] = PfxSumA[id.x] + ((id.x >= PfxSumPower) * PfxSumA[id.x - PfxSumPower]);
}

#pragma kernel ClearBuffers
[numthreads(256, 1, 1)]
void ClearBuffers (uint id : SV_DispatchThreadID)
{
	if (id.x >= NumParts)
		return;

	PartCounts[id.x] = 0;
	PfxSumA[id.x] = 0;
	PfxSumB[id.x] = 0;
}

#pragma kernel SortSpecks
[numthreads(256, 1, 1)]
void SortSpecks (uint id : SV_DispatchThreadID)
{
	if (id.x >= NumSpecks)
		return;

	int partIdx = PartIndices[id.x].x;
	int subPartIdx = PartIndices[id.x].y;
	int partOffset = PfxSumB[partIdx] - PartCounts[partIdx];

	int globalIdx = partOffset + subPartIdx;
	SortedSpecks[globalIdx] = Specks[id.x];
}
